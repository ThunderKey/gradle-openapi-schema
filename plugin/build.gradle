plugins {
    id 'java-gradle-plugin'
    id 'java-test-fixtures'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'openapischema.java'
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    implementation group: 'io.swagger.parser.v3', name: 'swagger-parser-v3', version: '2.1.19'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation "io.github.muehmar:pojo-builder-annotations:1.4.2"
    annotationProcessor "io.github.muehmar:pojo-builder:1.4.2"

    implementation "io.github.muehmar:code-generator:0.19.0"

    testImplementation testFixtures(project(":java-snapshot"))

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.11.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.11.0'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.11.0'

    def junitVersion = "5.10.1"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: junitVersion
    testRuntimeOnly files(createClasspathManifest)
}

group = 'com.github.muehmar.openapischema'

pluginBundle {
    website = "https://github.com/muehmar/gradle-openapi-schema"
    vcsUrl = "https://github.com/muehmar/gradle-openapi-schema.git"
    tags = ['java', 'openapi']
}

gradlePlugin {
    plugins {
        openApiSchemaPlugin {
            id = 'com.github.muehmar.openapischema'
            displayName = 'OpenApiSchema'
            description = 'Generate schema sources for Java from an OpenAPI 3.0 specification.'
            implementationClass = 'com.github.muehmar.gradle.openapi.OpenApiSchemaGenerator'
        }
    }
}

test {
    def updateSnapshot = project.properties["updateSnapshot"]
    if (updateSnapshot != null) {
        systemProperties.put("updateSnapshot", updateSnapshot)
    }
}

