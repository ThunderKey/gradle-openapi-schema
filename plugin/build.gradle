plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.19.0'
    id 'openapischema.java'
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    implementation group: 'io.swagger.parser.v3', name: 'swagger-parser-v3', version: '2.1.12'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    implementation "io.github.muehmar:pojo-builder-annotations:1.3.0"
    annotationProcessor "io.github.muehmar:pojo-builder:1.3.0"

    implementation "io.github.muehmar:code-generator:0.18.0"

    implementation 'io.github.origin-energy:java-snapshot-testing-junit5:4.0.6'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.11.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.11.0'

    def junitVersion = "5.9.2"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: junitVersion
    testRuntimeOnly files(createClasspathManifest)
}

group = 'com.github.muehmar.openapischema'

pluginBundle {
    website = "https://github.com/muehmar/gradle-openapi-schema"
    vcsUrl = "https://github.com/muehmar/gradle-openapi-schema.git"
    tags = ['java', 'openapi']
}

gradlePlugin {
    plugins {
        openApiSchemaPlugin {
            id = 'com.github.muehmar.openapischema'
            displayName = 'OpenApiSchema'
            description = 'Generate schema sources for Java from an OpenAPI 3.0 specification.'
            implementationClass = 'com.github.muehmar.gradle.openapi.OpenApiSchemaGenerator'
        }
    }
}

def snapshotPropertiesFile = "$projectDir/src/test/resources/snapshot.properties"

tasks.register("enableUpdateAllSnapshots") {
    doFirst {
        exec {
            commandLine 'sed', '-i', 's/update-snapshot=none/update-snapshot=all/', snapshotPropertiesFile
        }
    }
}

tasks.register("updateSnapshots", Test) {
    group 'verification'
    dependsOn(tasks.enableUpdateAllSnapshots)
    dependsOn(tasks.test) {
        useJUnitPlatform() {
            includeTags 'snapshot-test'
        }
    }
    doLast {
        exec {
            commandLine 'sed', '-i', 's/update-snapshot=all/update-snapshot=none/', snapshotPropertiesFile
        }
    }
}

