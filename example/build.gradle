plugins {
    id "com.github.muehmar.openapischema" version "2.4.0"
    id "com.diffplug.spotless"
    id 'openapischema.java'
    id 'java-test-fixtures'
}

sourceSets {
    generated {
        java {}
    }
    noJsonAndValidation {
        java {}
    }
    noJson {
        java {}
    }
    noValidation {
        java {}
    }
    test {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }

}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.15.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.15.3'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.15.3'

    noValidationImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.3'
    noValidationImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.15.3'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.10.1'

    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.2.5.Final'
    implementation group: 'javax.el', name: 'javax.el-api', version: '3.0.0'
    implementation group: 'org.glassfish', name: 'javax.el', version: '3.0.0'

    noJsonImplementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    testImplementation 'org.reflections:reflections:0.10.2'
    testImplementation testFixtures(project(":java-snapshot"))
}

def specifications =
        [
                [name: 'v1', file: 'openapi-v1.yml'],
                [name: 'v2', file: 'openapi-v2.yml'],
                [name: 'inlineobject', file: 'openapi-inline-object.yml'],
                [name: 'nullability', file: 'openapi-nullability.yml'],
                [name: 'array', file: 'openapi-array.yml'],
                [name: 'remoteref', file: 'openapi-remote-ref-main.yml'],
                [name: 'freeform', file: 'openapi-freeform.yml'],
                [name: 'validation', file: 'openapi-validation.yml'],
                [name: 'oneof', file: 'openapi-oneof.yml'],
                [name: 'anyof', file: 'openapi-anyof.yml'],
                [name: 'allof', file: 'openapi-allof.yml'],
                [name: 'identifiers', file: 'openapi-identifiers.yml'],
                [name: 'readwriteonly', file: 'openapi-read-only-write-only.yml'],
                [name: 'specv21', file: 'openapi-spec-version-v31.yml'],
                [name: 'fullobject', file: 'openapi-full-object.yml'],
                [name: 'additionalproperties', file: 'openapi-additional-properties.yml'],
                [name: 'sasis', file: 'sasis-api.json'],
                [name: 'promotion', file: 'openapi-promotion.yml'],
                [name: 'overrideglobalconfig', file: 'openapi-override-global-config.yml']
        ]

compileJava.dependsOn compileGeneratedJava
compileJava.dependsOn compileNoJsonJava
compileJava.dependsOn compileNoValidationJava
compileJava.dependsOn compileNoJsonAndValidationJava

openApiGenerator {
    sourceSet = 'main'
    outputDir = project.layout.buildDirectory.dir("generated/openapi")
    jsonSupport = "jackson"
    suffix = "Dto"
    enableValidation = true
    builderMethodPrefix = "set"

    enumDescriptionExtraction {
        enabled = true
        prefixMatcher = "`__ENUM__`:"
        failOnIncompleteDescriptions = true
    }

    formatTypeMapping {
        formatType = "username"
        classType = "com.package.UserName"
    }

    formatTypeMapping {
        formatType = "password"
        classType = "com.package.Password"
    }

    validationMethods {
        getterSuffix = "Raw"
    }

    warnings {
        disableWarnings = false
        failOnWarnings = true
        failOnUnsupportedValidation = true
    }

    schemas {
        apiV1 {
            inputSpec = "$projectDir/src/main/resources/openapi-v1.yml"
            packageName = "com.github.muehmar.gradle.openapi.v1"

            formatTypeMapping {
                formatType = "MedicationKind"
                classType = "com.github.muehmar.gradle.openapi.v1.MedicationKind"
            }

            classMapping {
                fromClass = "List"
                toClass = "java.util.ArrayList"
            }
        }
        apiV2 {
            inputSpec = "$projectDir/src/main/resources/openapi-v2.yml"
            packageName = "com.github.muehmar.gradle.openapi.v2"
            builderMethodPrefix = ""
        }
        inlineObject {
            inputSpec = "$projectDir/src/main/resources/openapi-inline-object.yml"
            packageName = "com.github.muehmar.gradle.openapi.inlineobject"
        }
        nullability {
            inputSpec = "$projectDir/src/main/resources/openapi-nullability.yml"
            packageName = "com.github.muehmar.gradle.openapi.nullability"

            validationMethods {
                getterSuffix = "Raw"
                modifier = "public"
                deprecatedAnnotation = true
            }
        }
        array {
            inputSpec = "$projectDir/src/main/resources/openapi-array.yml"
            packageName = "com.github.muehmar.gradle.openapi.array"
        }
        remoteRef {
            inputSpec = "$projectDir/src/main/resources/openapi-remote-ref-main.yml"
            packageName = "com.github.muehmar.gradle.openapi.remoteref"
        }
        excludeSchema {
            inputSpec = "$projectDir/src/main/resources/openapi-excluded-schema.yml"
            packageName = "com.github.muehmar.gradle.openapi.excludedschema"
            excludeSchemas = ["User"] // This schema contains an unsupported URL-reference
        }
        parameters {
            inputSpec = "$projectDir/src/main/resources/openapi-parameters.yml"
            packageName = "com.github.muehmar.gradle.openapi.parameters"
        }
        freeForm {
            inputSpec = "$projectDir/src/main/resources/openapi-freeform.yml"
            packageName = "com.github.muehmar.gradle.openapi.freeform"
        }
        validation {
            inputSpec = "$projectDir/src/main/resources/openapi-validation.yml"
            packageName = "com.github.muehmar.gradle.openapi.validation"

            formatTypeMapping {
                formatType = "name"
                classType = "com.github.muehmar.gradle.openapi.validation.Name"
            }

            warnings {
                failOnUnsupportedValidation = false
            }
        }
        oneOf {
            inputSpec = "$projectDir/src/main/resources/openapi-oneof.yml"
            packageName = "com.github.muehmar.gradle.openapi.oneof"
        }
        anyOf {
            inputSpec = "$projectDir/src/main/resources/openapi-anyof.yml"
            packageName = "com.github.muehmar.gradle.openapi.anyof"
        }
        allOf {
            inputSpec = "$projectDir/src/main/resources/openapi-allof.yml"
            packageName = "com.github.muehmar.gradle.openapi.allof"
        }
        identifiers {
            inputSpec = "$projectDir/src/main/resources/openapi-identifiers.yml"
            packageName = "com.github.muehmar.gradle.openapi.identifiers"
            suffix = ""
        }
        readOnlyWriteOnly {
            inputSpec = "$projectDir/src/main/resources/openapi-read-only-write-only.yml"
            packageName = "com.github.muehmar.gradle.openapi.readwriteonly"
        }
        specVersionV31 {
            inputSpec = "$projectDir/src/main/resources/openapi-spec-version-v31.yml"
            packageName = "com.github.muehmar.gradle.openapi.specversionv31"
        }
        fullObject {
            inputSpec = "$projectDir/src/main/resources/openapi-full-object.yml"
            packageName = "com.github.muehmar.gradle.openapi.fullobject"
        }
        additionalProperties {
            inputSpec = "$projectDir/src/main/resources/openapi-additional-properties.yml"
            packageName = "com.github.muehmar.gradle.openapi.additionalproperties"
        }
        sasis {
            inputSpec = "$projectDir/src/main/resources/sasis-api.json"
            packageName = "com.github.muehmar.gradle.openapi.sasis"

            formatTypeMapping {
                formatType = "date-time"
                classType = "java.time.LocalDate"
            }

            constantSchemaNameMapping {
                constant = "Sasis.Register.CareProvider.Common.Models.ApiGateway.V1."
                replacement = ""
            }

            constantSchemaNameMapping {
                constant = "."
                replacement = ""
            }
        }
        promotion {
            inputSpec = "$projectDir/src/main/resources/openapi-promotion.yml"
            packageName = "com.github.muehmar.gradle.openapi.promotion"
        }
        overrideGlobalConfig {
            sourceSet = "generated"
            inputSpec = "$projectDir/src/main/resources/openapi-override-global-config.yml"
            outputDir = project.layout.buildDirectory.dir("generated/openapi-override")
            packageName = "com.github.muehmar.gradle.openapi.overrideglobalconfig"
            suffix = ""
            jsonSupport = "none"
            enableValidation = false
            builderMethodPrefix = ""

        }
        ['71', '182', '185', '190', '191', '192', '193', '195'].forEach {issueNumber -> {
            "${issueNumber}" {
                inputSpec = "$projectDir/src/main/resources/issues/openapi-issue-${issueNumber}.yml"
                packageName = "com.github.muehmar.gradle.openapi.issue${issueNumber}"
            }
        }}

        specifications.forEach {spec -> {
            "noValidation${spec.name}" {
                sourceSet = "noValidation"
                outputDir = project.layout.buildDirectory.dir("generated/openapi-no-validation")
                inputSpec = "$projectDir/src/main/resources/${spec.file}"
                packageName = "${project.group}.${project.name}.api.novalidation.${spec.name}.model"
                enableValidation = false
            }
        }}

        specifications.forEach {spec -> {
            "noJsonSupport${spec.name}" {
                sourceSet = "noJson"
                outputDir = project.layout.buildDirectory.dir("generated/openapi-no-json")
                inputSpec = "$projectDir/src/main/resources/${spec.file}"
                packageName = "${project.group}.${project.name}.api.nojsonsupport.${spec.name}.model"
                jsonSupport = "none"
            }
        }}

        specifications.forEach {spec -> {
            "noJsonSupportAndValidation${spec.name}" {
                sourceSet = "noJsonAndValidation"
                outputDir = project.layout.buildDirectory.dir("generated/openapi-no-json-no-validation")
                inputSpec = "$projectDir/src/main/resources/${spec.file}"
                packageName = "${project.group}.${project.name}.api.nojsonsupportandvalidation.${spec.name}.model"
                jsonSupport = "none"
                enableValidation = false
            }
        }}
    }
}

afterEvaluate {
    tasks.named("generateRemoteRefModel") {
        inputs.file("$projectDir/src/main/resources/openapi-remote-ref-sub.yml")
    }
}

test {
    def updateSnapshot = project.properties["updateSnapshot"]
    if (updateSnapshot != null) {
        systemProperties.put("updateSnapshot", updateSnapshot)
    }
}

